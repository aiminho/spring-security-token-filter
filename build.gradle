buildscript {
  repositories {
    mavenCentral()
    mavenLocal()
  }
}

plugins {
  id 'org.springframework.boot' version '2.0.1.RELEASE'
}

def samplesContainerProjectName = 'samples'
def ossrhUser = hasProperty('ossrhUsername') ? ossrhUsername : ''
def ossrhPass = hasProperty('ossrhPassword') ? ossrhPassword : ''

subprojects {
  if (it.parent != null && it.name != samplesContainerProjectName) {
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'

    repositories {
      mavenCentral()
    }

    dependencyManagement {
      imports {
        mavenBom 'org.springframework.boot:spring-boot-dependencies:2.0.1.RELEASE'
      }
    }

    if (it.parent.name == samplesContainerProjectName) {
      // For sample projects
      apply plugin: 'java'

    } else {
      // for the library projects
      apply plugin: 'jacoco'
      apply plugin: 'maven'
      apply plugin: 'signing'
      apply plugin: 'java-library'

      repositories {
        mavenLocal()
      }

      afterEvaluate { Project project ->
        signing {
          sign configurations.archives
        }

        task javadocJar(type: Jar) {
          classifier = 'javadoc'
          from javadoc
        }

        task sourcesJar(type: Jar) {
          classifier = 'sources'
          from sourceSets.main.allSource
        }

        artifacts {
          archives javadocJar, sourcesJar
        }

        uploadArchives {
          repositories {
            mavenDeployer {
              beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

              repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUser, password: ossrhPass)
              }

              snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUser, password: ossrhPass)
              }

              pom.project {
                name project.name
                packaging 'jar'
                description project.description
                url 'https://github.com/visola/spring-security-token-filter'

                scm {
                  connection 'https://github.com/visola/spring-security-token-filter.git'
                  developerConnection 'https://github.com/visola/spring-security-token-filter.git'
                  url 'https://github.com/visola/spring-security-token-filter.git'
                }

                licenses {
                  license {
                    name 'The MIT License (MIT)'
                    url 'https://opensource.org/licenses/MIT'
                  }
                }

                developers {
                  developer {
                    id 'visola'
                    name 'Vinicius Isola'
                    email 'viniciusisola@gmail.com'
                  }
                }
              }
            }
          }
        }
      }
    }
  }
}
